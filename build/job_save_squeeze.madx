option, warn,info;
system,"rm -rf temp"; system,"mkdir temp";
!system,"ln -fns /afs/cern.ch/eng/lhc/optics/HLLHCV1.2 slhc";
system,"ln -fns .. slhc";
system,"ln -fns /afs/cern.ch/eng/lhc/optics/V6.503 db5";
option,-echo,-info,-warn,no_fatal_stop;

call,file="slhc/toolkit/macro.madx";

Option, -echo,-warn,-info;
REAL CONST l.TAN   = 3.7  ;REAL CONST l.TANAL = l.TAN;
REAL CONST l.TANAR = l.TAN;REAL CONST l.TANC  = l.TAN;
REAL CONST l.TCT   = 1.0;REAL CONST l.TCTH  = l.TCT;REAL CONST l.TCTVA = l.TCT;
call,file="db5/V6.5.as-built.seq";

Option, -echo,warn,-info;

call,file="opt_presqueeze_new.madx";
call,file="slhc/hllhc_sequence.madx";
exec,crossing_disable;
exec,mk_beam(7000);
exec,check_ip(b1); exec,check_ip(b2);

readmytable,table=ir2_round    ,file="ir2_round.tfs";
readmytable,table=ir4_round    ,file="ir4_round.tfs";
readmytable,table=ir6_round    ,file="ir6_round.tfs";
readmytable,table=ir8_round    ,file="ir8_round.tfs";
readmytable,table=ir2_flat     ,file="ir2_flat.tfs";
readmytable,table=ir4_flat     ,file="ir4_flat.tfs";
readmytable,table=ir6_flat     ,file="ir6_flat.tfs";
readmytable,table=ir8_flat     ,file="ir8_flat.tfs";
readmytable,table=ir2_flathv   ,file="ir2_flathv.tfs";
readmytable,table=ir4_flathv   ,file="ir4_flathv.tfs";
readmytable,table=ir6_flathv   ,file="ir6_flathv.tfs";
readmytable,table=ir8_flathv   ,file="ir8_flathv.tfs";

saveopt(nnn,aaa,bbb,ccc,ddd): macro={
exec,check_ip(b1); exec,check_ip(b2);
call,file="slhc/toolkit/clean_specs.madx";
call,file="slhc/toolkit/rematch_squeeze.madx";
if (tarsqueeze>1e-15) { return;};
call,file="slhc/toolkit/rematch_globalw.madx";
Psep=0.75e-3;Xang=0.000275;
call,file="slhc/toolkit/rematch_crossing.madx";
call,file="slhc/toolkit/rematch_crabs.madx";
if (tarcrossing>1e-15) { return;};
if (tarsqueeze+tarir37b12+tarcrossing<1e-15){
arc_squeeze=1;on_check=1;call,file="slhc/toolkit/save_optics2.madx";
system,"cp temp/optics.madx opt_nnn_aaa_bbb_ccc_ddd.madx";
};
};

!return;
!! round
!call,file="opt_presqueeze_new.madx";
!iii=1; iiistop=table(ir2_round,tablelength);
!value,iii,iiistop;
!while(iii<=iiistop){
!setvars,table=ir2_round,row=iii;
!setvars,table=ir4_round,row=iii;
!setvars,table=ir6_round,row=iii;
!setvars,table=ir8_round,row=iii;
!value,betx_ip1,betx_ip5;
!aaa=round(betx_ip1*1e3); bbb=round(bety_ip1*1e3);
!ccc=round(betx_ip5*1e3); ddd=round(bety_ip5*1e3);
!exec,saveopt(round,$aaa,$bbb,$ccc,$ddd);
!iii=iii+1;
!};

! flat
call,file="opt_presqueeze_new.madx";
iii=1; iiistop=table(ir2_flat,tablelength);
value,iii,iiistop;
while(iii<=iiistop){
setvars,table=ir2_flat,row=iii;
setvars,table=ir4_flat,row=iii;
setvars,table=ir6_flat,row=iii;
setvars,table=ir8_flat,row=iii;
value,betx_ip1,betx_ip5,bety_ip1,bety_ip5;
aaa=round(betx_ip1*1e3); bbb=round(bety_ip1*1e3);
ccc=round(betx_ip5*1e3); ddd=round(bety_ip5*1e3);
exec,saveopt(flat,$aaa,$bbb,$ccc,$ddd);
iii=iii+1;
if (aaa==bbb){iii=1000000;};
};

! flathv
call,file="opt_presqueeze_new.madx";
exec,check_ip(b1); exec,check_ip(b2);
iii=1; iiistop=table(ir2_flathv,tablelength);
value,iii,iiistop;
while(iii<=iiistop){
setvars,table=ir2_flathv,row=iii;
setvars,table=ir4_flathv,row=iii;
setvars,table=ir6_flathv,row=iii;
setvars,table=ir8_flathv,row=iii;
value,betx_ip1,betx_ip5,bety_ip1,bety_ip5;
aaa=round(betx_ip1*1e3); bbb=round(bety_ip1*1e3);
ccc=round(betx_ip5*1e3); ddd=round(bety_ip5*1e3);
exec,saveopt(flathv,$aaa,$bbb,$ccc,$ddd);
iii=iii+1;
if (aaa==bbb){iii=1000000;};
};

return;
