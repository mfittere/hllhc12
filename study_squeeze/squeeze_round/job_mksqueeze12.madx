option, warn,info;
!system,"ln -fns /afs/cern.ch/eng/lhc/optics/HLLHCV1.2/ slhc";
system,"ln -fns .. slhc";
system,"ln -fns /afs/cern.ch/eng/lhc/optics/V6.503 db5";
system,"mkdir temp";

option,-echo,-info,-warn;
option, -echo,-warn,-info,no_fatal_stop;
REAL CONST l.TAN   = 3.7  ;REAL CONST l.TANAL = l.TAN;
REAL CONST l.TANAR = l.TAN;REAL CONST l.TANC  = l.TAN;
REAL CONST l.TCT   = 1.0;REAL CONST l.TCTH  = l.TCT;REAL CONST l.TCTVA = l.TCT;
REAL CONST l.MBAS2             =0 ;
REAL CONST l.MBAW              =0 ;
REAL CONST l.MBCS2             =0 ;
REAL CONST l.MBLS2             =0 ;
REAL CONST l.MBLW              =0 ;
REAL CONST l.MBWMD             =0 ;
REAL CONST l.MBXWH             =0 ;
REAL CONST l.MBXWS             =0 ;
REAL CONST l.MBXWT             =0 ;

!call,file="db5/V6.5.seq";
call,file="db5/V6.5.as-built.seq";
!call,file="/afs/cern.ch/eng/lhc/optics/runII/2015/lhc_as-built.seq";
Option, -echo,warn,-info;

call,file="slhc/toolkit/macro.madx";
call,file="slhc/hllhc_sequence.madx";
exec,mk_beam(7000);
call,file="rematchir6/opt_presqueeze_new.madx";
exec, crossing_disable;
exec,check_ip(b1); exec,check_ip(b2);
call,file="toolkit/optics_log.madx";

!squeeze3
readmytable,table=ir2_round,file="squeeze_round/squeeze3/ir2_round.tfs";
readmytable,table=ir4_round,file="squeeze_round/squeeze3/ir4_round.tfs";
!squeeze2
readmytable,table=ir8_round,file="squeeze_round/squeeze2/ir8_round.tfs";
!take table from squeeze4b
readmytable,table=ir6_round,file="squeeze_round/squeeze11/ir6_log_str_nokqt.tfs";
!create squeeze tables
exec,make_opticstbl_ir2(ir2);
exec,make_opticstbl_ir4(ir4);
exec,make_opticstbl_ir6(ir6);
exec,make_opticstbl_ir8(ir8);
on_opticstbl_ir2 = 1 ;
on_opticstbl_ir4 = 1 ;
on_opticstbl_ir6 = 1 ;
on_opticstbl_ir8 = 1 ;

set_beta_const: macro ={
  betx0_ip1=0.48;bety0_ip1=0.48;betx0_ip5=0.48;bety0_ip5=0.48;
  betx_ip2=10.0;bety_ip2=10.0;alfx_ip2=0;alfy_ip2=0;
  betx_ip8=3.0;bety_ip8=3.0;alfx_ip8=0;alfy_ip8=0;
  alfx_ip1=0;alfy_ip1=0; 
  alfx_ip5=0;alfy_ip5=0;
};

mk_squeeze: macro={
sss=0.005;!0.5 cm squeeze steps
eee=table(ir6_round,tablelength);
rrr=eee-1;
k:=rrr-1;
value,sss,rrr,eee;
while(rrr>=1){
  bet12=0.15+sss*(rrr-1);!beta* v12 tables
  setvars,table=ir2_round,row=rrr;
  setvars,table=ir4_round,row=rrr;
  setvars,table=ir6_round,row=rrr;
  setvars,table=ir8_round,row=rrr;
  value, bet12,bet12/0.48;
  value, bet12,bet12/0.48;
  exec, set_beta_const;
  betx_ip1=bet12;bety_ip1=bet12;
  betx_ip5=bet12;bety_ip5=bet12;
!fit twiss parameters at IP6 with polynomial
!where start and end condition are fixed
  muxip6b1 = 2.0000 ;muyip6b1 = 2.0300 ;muxip6b2 = 2.0000 ;muyip6b2 = 2.0300 ;
  if(k<15){
    call,file="squeeze_round/fit_squeeze12_idx_0_14.madx";
  };
  else {
    call,file="squeeze_round/fit_squeeze12_idx_15_66.madx";
  };
  jac_calls=15;
  call,file="toolkit/rematch_squeeze.madx";
  value, betx_ip1;
  if(tarsqueeze>1.e-15){return;};
  exec,store_optics(ir2);
  exec,store_optics(ir4);
  exec,store_optics(ir6);
  exec,store_optics(ir8);
  rrr=rrr-1;
};
if(tarsqueeze>1.e-15){return;};
value,betx_ip1,betx_ip5;
value,ttt,rrr;
};

call,file="rematchir6/opt_presqueeze_new.madx";
exec,check_ip(b1);exec,check_ip(b2);
jac_calls=0;
call,file="toolkit/rematch_squeeze.madx";
exec,store_optics(ir2);
exec,store_optics(ir4);
exec,store_optics(ir6);
exec,store_optics(ir8);
exec, mk_squeeze;

exec,check_ip(b1); exec,check_ip(b2);

return;



!save optics
save_optics(bbb,ccc): macro={
exec,check_ip(b1); exec,check_ip(b2);
call,file="toolkit/rematch_globalw.madx";
call,file="toolkit/rematch_crossing.madx";
if (tarcrossing>1e-15) { return;};
call,file="toolkit/rematch_crabs.madx";
call,file="toolkit/mk_ir28_knobs.madx";
on_check=1; call,file="toolkit/save_optics2.madx";
system,"cp temp/optics.madx squeeze/opt_bbb_ccc_ccc_bbb.madx";
};

bet1=betx_ip1*1000;bet2=bety_ip1*1000;
exec, save_optics(bet1,bet2);
